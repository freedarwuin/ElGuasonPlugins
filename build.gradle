plugins {
    id 'java'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.runelite.net'
        }
        mavenCentral()
        jcenter()
    }

    def runeLiteVersion = '1.10.32'

    plugins.withType(JavaPlugin) {
        dependencies {
            compileOnly 'org.projectlombok:lombok:1.18.20'
            annotationProcessor 'org.projectlombok:lombok:1.18.20'
            compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
            testImplementation 'junit:junit:4.12'
            testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
            testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion
        }
    }
}

dependencies {
    implementation project(path: ':EthansApi')
    implementation project(path: ':ChinBreakHandler')
    implementation project(path: ':PiggyUtils')
    implementation project(path: ':PlankBuddy')
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
}

sourceSets {
    main {
        java {
            srcDir "src"
        }
    }
}

group = 'com.spinplugins'
version = '1.0.0'
sourceCompatibility = "11"

tasks.register('PluginJar', Jar) {
    manifest {
        attributes 'Main-Class': 'com.spinplugins.Main'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

subprojects {
    tasks.withType(Jar).tap {
        configureEach {
            doLast {
                copy {
                    from("./build/libs/")
                    into("../release/")
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
targetCompatibility = JavaVersion.VERSION_11
